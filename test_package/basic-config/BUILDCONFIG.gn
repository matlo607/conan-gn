# All targets will get this list of configs by default.
# Targets can opt out of a config by removing it from their local configs list.
# If you're adding global flags and don't need targets to be able to opt out,
# add the flags to compiler_defaults, not to a new config.
base_target_configs = [
  "//basic-config:compiler_defaults",
  "//basic-config:no_rtti",
  "//basic-config:position_independent_code",
]

shared_binary_target_configs = base_target_configs + [
  "//basic-config:check_undefined_symbols"
]

# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = base_target_configs
}
set_defaults("loadable_module") {
  configs = base_target_configs
}
set_defaults("static_library") {
  configs = base_target_configs
}
set_defaults("shared_library") {
  configs = shared_binary_target_configs
}
set_defaults("source_set") {
  configs = base_target_configs
}

if (target_os == "") {
  target_os = host_os
}
if (current_os == "") {
  current_os = target_os
}

if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}

if (host_os == "win") {
  host_toolchain = "//basic-config/toolchain:win"
} else {
  host_toolchain = "//basic-config/toolchain:unix"
}

set_default_toolchain(host_toolchain)

is_linux = current_os == "linux"
is_mac = current_os == "mac"
is_win = current_os == "win"

# Tests configuration
if (is_win) {
  test_wrapper_lang = "powershell"
  #test_wrapper_lang = "batch"
} else {
  test_wrapper_lang = "bash"
  #test_wrapper_lang = "powershell"
}

if (test_wrapper_lang == "batch") {
  test_wrapper_extension = ".bat"
} else if (test_wrapper_lang == "bash") {
  test_wrapper_extension = ".sh"
} else if (test_wrapper_lang == "powershell") {
  test_wrapper_extension = ".ps1"
}